// Kotlin stuff
buildscript {
    ext.kotlin_version = '1.2.0-beta-31'
    ext.dokka_version = '0.9.15'
    ext.shadow_version = '2.0.1'

    repositories {
        mavenCentral()
        jcenter()

        maven {
            url "http://dl.bintray.com/kotlin/kotlin-eap-1.2"
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
        classpath "com.github.jengelman.gradle.plugins:shadow:$shadow_version"
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'idea'

    group = 'chattime'
    version = projectVersion

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()

        maven {
            url "http://dl.bintray.com/kotlin/kotlin-eap-1.2"
        }
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jre8"
    }
}

project(":server") {
    apply plugin: 'org.jetbrains.dokka'

    sourceSets {
        api
    }

    dokka {
        outputFormat = 'html'
        outputDirectory = "$buildDir/javadoc"

        kotlinTasks {
            [compileApiKotlin]
        }
    }

    dependencies {
        compile sourceSets.api.output
        compile "io.reactivex.rxjava2:rxkotlin:$rxKotlinVersion"
        apiCompile "org.jetbrains.kotlin:kotlin-stdlib-jre8"
    }

    jar {
        from sourceSets.main.output
        from sourceSets.api.output
    }

    task apiJar(type: Jar) {
        classifier = 'api'

        from sourceSets.api.output
    }

    apiJar.baseName = "ChatTime-${project.name}"
}

project(":client") {
    dependencies {
        compile "no.tornado:tornadofx:$tornadoFxVersion"
    }
}

subprojects {
    dependencies {
        compile project(':')
        compile "info.picocli:picocli:$picocliVersion"
    }

    apply plugin: 'com.github.johnrengelman.shadow'

    jar {
        manifest {
            attributes 'Main-Class': "chattime.${project.name}.MainKt"
        }
    }

    jar.baseName = "ChatTime-${project.name}"
    shadowJar.baseName = "ChatTime-${project.name}"
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.1'
}
