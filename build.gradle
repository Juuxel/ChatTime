// Kotlin stuff
buildscript {
    ext.kotlin_version = '1.1.4'
    ext.dokka_version = '0.9.15'
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'idea'
    apply plugin: 'org.jetbrains.dokka'

    group = 'chattime'
    version =  '0.1.0'

    sourceCompatibility = 1.8

    repositories {
        mavenCentral()
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jre8"
    }

    dokka {
        outputFormat = 'html'
        outputDirectory = "$buildDir/javadoc"
    }
}

project(':server') {
    dependencies {
        compile "org.jetbrains.kotlin:kotlin-reflect"
    }
}

subprojects {
    dependencies {
        compile project(':')
    }

    task fatJar(type: Jar) {
        from compileKotlin

        // https://stackoverflow.com/a/3450409
        from configurations.compile.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }

    jar {
        manifest {
            attributes 'Main-Class': "chattime.${project.name}.MainKt"
        }
    }

    fatJar {
        manifest {
            attributes 'Main-Class': "chattime.${project.name}.MainKt"
        }
    }

    jar.baseName = "ChatTime-${project.name}"
    fatJar.baseName = "ChatTime-${project.name}-fat"
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.1'
}
