// Kotlin stuff
buildscript {
    ext.kotlin_version = '1.2.51'
    ext.dokka_version = '0.9.17'
    ext.shadow_version = '2.0.1'

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
        classpath "com.github.jengelman.gradle.plugins:shadow:$shadow_version"
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'kotlin'

    group = 'chattime'
    version = projectVersion

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        compile "com.beust:klaxon:$klaxonVersion"
        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    }
}

project(":api") {
    apply plugin: 'org.jetbrains.dokka'

    dokka {
        outputFormat = 'html'
        outputDirectory = "$buildDir/javadoc"
    }
}

project(":server") {
    dependencies {
        compile "io.reactivex.rxjava2:rxjava:$rxJavaVersion"
    }
}

project(":") {
    dependencies {
        compile project(':api')
    }
}

configure(subprojects.findAll { it.name == 'client' || it.name == 'server' }) {
    dependencies {
        compile project(':')
        compile project(':api')
        compile "info.picocli:picocli:$picocliVersion"
    }

    apply plugin: 'com.github.johnrengelman.shadow'

    jar {
        manifest {
            attributes 'Main-Class': "chattime.${project.name}.MainKt"
        }
    }

    jar.baseName = "ChatTime-${project.name}"
    shadowJar.baseName = "ChatTime-${project.name}"
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.6'
}
