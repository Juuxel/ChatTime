// Kotlin stuff
buildscript {
    ext.kotlin_version = '1.2-M2'
    ext.dokka_version = '0.9.15'
    ext.shadow_version = '2.0.1'

    repositories {
        mavenCentral()
        jcenter()

        maven {
            url "http://dl.bintray.com/kotlin/kotlin-eap-1.2"
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
        classpath "com.github.jengelman.gradle.plugins:shadow:$shadow_version"
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'idea'
    apply plugin: 'org.jetbrains.dokka'

    group = 'chattime'
    version =  '0.1.0'

    sourceCompatibility = 1.8

    repositories {
        mavenCentral()

        maven {
            url "http://dl.bintray.com/kotlin/kotlin-eap-1.2"
        }
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jre8"
    }

    dokka {
        outputFormat = 'html'
        outputDirectory = "$buildDir/javadoc"
    }
}

project(":server") {
    sourceSets {
        api
    }

    dependencies {
        compile sourceSets.api.output
        apiCompile "org.jetbrains.kotlin:kotlin-stdlib-jre8"
    }

    jar {
        from sourceSets.main.output
        from sourceSets.api.output
    }

    task apiJar(type: Jar) {
        classifier = 'api'

        from sourceSets.api.output
    }
}

subprojects {
    dependencies {
        compile project(':')
        compile "info.picocli:picocli:$picocliVersion"
    }

    apply plugin: 'com.github.johnrengelman.shadow'

    jar {
        manifest {
            attributes 'Main-Class': "chattime.${project.name}.MainKt"
        }
    }

    jar.baseName = "ChatTime-${project.name}"
    shadowJar.baseName = "ChatTime-${project.name}"
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.1'
}
